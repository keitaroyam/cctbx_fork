Import("env_base", "env_etc")

env_etc.iotbx_dist = env_etc.libtbx_env.dist_path("iotbx")
env_etc.iotbx_include = env_etc.norm_join(env_etc.iotbx_dist, "include")

env_etc.iotbx_common_includes = [
  env_etc.libtbx_include,
  env_etc.iotbx_include,
  env_etc.cctbx_include,
  env_etc.scitbx_include,
  env_etc.boost_include,
  env_etc.ccp4io_include,
]

myccflags_base = env_etc.ccflags_base[:]
myccflags_base.extend(env_etc.ccp4io_defines)
mycxxflags_base = env_etc.cxxflags_base[:]
mycxxflags_base.extend(env_etc.ccp4io_defines)

mtz_env = env_base.Copy(
  CXXFLAGS=mycxxflags_base,
  SHCXXFLAGS=mycxxflags_base,
  SHLINKFLAGS=env_etc.shlinkflags,
  CPPPATH=env_etc.iotbx_common_includes,
)

envlm = mtz_env.Copy(
  LIBS=env_etc.libm
)

lib_iotbx_sources = [
  "mtz/cppmtz.cpp",
  "mtz/mtzwriter.cpp",
]

envlm.Prepend(LIBS=["cmtz"])
envlm.Append(LIBPATH=["#libtbx",])

envlm.StaticLibrary(
  target="#libtbx/iotbx",
  source=lib_iotbx_sources)

Export("mtz_env")
SConscript("mtz/SConscript")

if (not env_etc.no_boost_python):
  Import("env_boost_python_ext")

  env_iotbx_boost_python_ext = env_boost_python_ext.Copy()
  env_iotbx_boost_python_ext.Replace(
    SHCXXFLAGS=mycxxflags_base,)

  env_iotbx_boost_python_ext.Replace(
    CPPPATH=env_etc.iotbx_common_includes + [env_etc.python_include])

  Export("env_iotbx_boost_python_ext")

  env_iotbx_boost_python_ext.Command(
    "#libtbx/iotbx_boost/__init__.py",
    "#libtbx/libtbx/touch_init.py", env_etc.touch_init)

  SConscript("detectors/SConscript")
  SConscript("mtz/boost_python/SConscript")
  SConscript("xplor/boost_python/SConscript")
  SConscript("misc/boost_python/SConscript")
