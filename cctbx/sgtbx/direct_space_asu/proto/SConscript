import os
import libtbx
Import("env_base", "env_etc")
env = env_base.Copy(
  CXXFLAGS=env_etc.cxxflags_base,
  LIBS=["cctbx"]+env_etc.libm,
  LIBPATH=["#lib"]
)

# With recent gcc:
#    -std=c++98 -Wall -Wextra -Wshorten-64-to-32 -Wconversion -Wcast-align -Wwrite-strings -Wcast-qual -pedantic 

flg = os.environ.get("CXXFLAGS")
if flg  is not None:
  flg = flg.split(" ")
  env.Append( CXXFLAGS=flg )

env_etc.include_registry.append(
  env=env,
  paths=env_etc.cctbx_common_includes)

envlm = env.Copy(
  LIBS=env_etc.libm
)

lib_asu_sources = [
  "cut.cpp",
  "reference_table.cpp",
  "direct_space_asu.cpp"
]

if (env_etc.static_libraries): builder = envlm.StaticLibrary
else:                          builder = envlm.SharedLibrary

builder(
  target="#lib/cctbx_sgtbx_asu",
  source=lib_asu_sources)


env.Prepend(LIBS=["cctbx_sgtbx_asu"])
env.Program( target = "tst_asu",  source="tst_asu.cpp")


if (not env_etc.no_boost_python):
  Import("env_cctbx_boost_python_ext")
  env_bpl = env_cctbx_boost_python_ext.Copy()
  env_etc.include_registry.append(
    env=env_bpl,
    paths=env_etc.cctbx_common_includes)
  env_bpl.Prepend(LIBS=["cctbx", "cctbx_sgtbx_asu"])
  env_bpl.SharedLibrary(
    target="#lib/cctbx_sgtbx_asu_ext",
    source=["asu_ext.cpp"])

